plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        maven {
            url 'https://jitpack.io'
            content {
                includeGroupByRegex "com\\.github.*"
            }
        }
        jcenter {
            content {
                excludeGroupByRegex "com\\.github.*"
            }
        }
    }

    dependencyManagement {
        overriddenByDependencies = false

        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.12.RELEASE'
        }

        testCompile {
            imports {
                mavenBom 'org.testcontainers:testcontainers-bom:1.12.5'
            }
        }

        dependencies {
            dependencySet(group: 'com.github.bsideup.liiklus', version: '0.9.3') {
                entry 'client'
                entry 'testing'
            }

            dependency 'com.github.VivyTeam.mdc-logger:logger:0.0.3'
            
            dependency 'com.github.VivyTeam:json-schema-merger:0.1.3'

            dependency 'org.awaitility:awaitility:3.1.6'

            dependency 'com.auth0:java-jwt:3.9.0'

            dependency 'com.avast.grpc.jwt:grpc-java-jwt:0.2.0'
        }
    }
}

subprojects {
    targetCompatibility = sourceCompatibility = 11

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }
}
