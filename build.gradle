plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE" apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        maven {
            url 'https://jitpack.io'
            content {
                includeGroupByRegex "com\\.github.*"
            }
        }
        jcenter {
            content {
                excludeGroupByRegex "com\\.github.*"
            }
        }
    }

    dependencyManagement {
        overriddenByDependencies = false

        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.3.RELEASE'
        }

        testCompile {
            imports {
                mavenBom 'org.testcontainers:testcontainers-bom:1.10.6'
            }
        }

        dependencies {
            dependencySet(group: 'com.github.bsideup.liiklus', version: '0.8.1') {
                entry 'client'
                entry 'testing'
            }

            dependency 'com.github.UvitaTeam.mdc-logger:logger:0.0.3'
            
            dependency 'com.github.UvitaTeam:json-schema-merger:0.1.3'

            dependency 'org.awaitility:awaitility:3.1.6'
        }
    }
}

subprojects {
    targetCompatibility = sourceCompatibility = 11

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }
}
