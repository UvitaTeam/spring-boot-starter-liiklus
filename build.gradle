plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE" apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    dependencyManagement {
        overriddenByDependencies = false

        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.3.RELEASE'
        }

        testCompile {
            imports {
                mavenBom 'org.testcontainers:testcontainers-bom:1.10.5'
            }
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.18.4'

            dependencySet(group: 'com.github.bsideup.liiklus', version: '0.7.0') {
                entry 'client'
                entry 'testing'
            }

            dependency 'io.rsocket:rsocket-transport-netty:0.11.15'

            dependency 'com.github.UvitaTeam.mdc-logger:logger:0.0.3'
            dependency 'com.github.UvitaTeam:json-schema-merger:0.1.3'

            dependency 'org.awaitility:awaitility:3.1.6'
        }
    }
}

subprojects {
    targetCompatibility = sourceCompatibility = 11

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }
}
